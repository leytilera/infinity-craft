import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.2.0'
}

apply plugin: "net.minecraftforge.gradle.forge"


def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getAPIKey = { ->
    def stdout = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        exec {
            commandLine 'cat', '../apiKey'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    return '0'
}

version = getVersionName()
group = modGroup
archivesBaseName = modBaseName
String apiToken = getAPIKey()

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    replace '${version}', project.version

}


repositories {
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "DVS1 Maven FS"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }

    maven { url "https://jitpack.io" }
    maven { url "https://maven.latmod.com/" }


}

dependencies {
    deobfCompile 'com.gitlab.lcoremodders:LucraftCore:85e6c788'
    deobfCompile 'com.gitlab.lcoremodders:HeroesExpansion:1.3.3'
    deobfCompile 'com.gitlab.lcoremodders:SpeedsterHeroes:1e4b1fb951'
    deobfCompile "star-tech-man-the-legendary-mod:star:tech:1.1.1"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}



task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLCorePlugin': 'lucraft.mods.lucraftcore.core.LucraftCoreCoreMod', 'FMLCorePluginContainsFMLMod': 'true'
    }

}

artifacts {
    archives deobfJar
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", getVersionName
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": getVersionName, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

curseforge {
    apiKey = apiToken
    project {
        id = projectID
        changelog = file('changelog.txt')
        releaseType = "beta"
        addGameVersion '1.12.2'

        mainArtifact(jar) {
            displayName = "Infinity Craft $getVersionName"
            relations {
                requiredDependency 'lucraft-core'
                optionalDependency 'heroesexpansion'
            }
        }
    }
}


